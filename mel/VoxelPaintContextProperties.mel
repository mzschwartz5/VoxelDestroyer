// This procedure has to have this specific name to be recognized by Maya
// See docs here: https://help.autodesk.com/view/MAYAUL/2022/ENU/?guid=Maya_SDK_Command_plug_ins_Tool_property_sheets_html
global proc VoxelPaintContextProperties() {
    // Required: parent into the Tool Settings property sheet
    string $parent = `toolPropertyWindow -q -location`;

    if (`control -exists $parent`)
        setParent $parent;

    setUITemplate -pushTemplate ToolOptionBoxTemplate;

    columnLayout VoxelPaintContext;

        frameLayout -collapsable false -label "Voxel Paint Settings";
            columnLayout;

                separator -style "none";

                floatSliderGrp -label "Radius"
                    -field true
                    -minValue 0.001
                    -maxValue 200.0
                    -fieldMinValue 0.001
                    -fieldMaxValue 1000.0
                    -value 25.0
                    -adjustableColumn 3
                    -annotation "Brush size in pixels"
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintRadiusControl;

                optionMenuGrp -label "Mode"
                    -adjustableColumn 2
                    -annotation "How a brush stroke affects paint values"
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintModeControl;

                    menuItem -label "Subtract";
                    menuItem -label "Set";
                    menuItem -label "Add";

                floatSliderGrp -label "Strength"
                    -field true
                    -minValue 0.000
                    -maxValue 100.0
                    -fieldMinValue 0.000
                    -fieldMaxValue 100.0
                    -value 50.0
                    -adjustableColumn 3
                    -annotation "Brush strength: a percentage of the low-to-high constraint value range set on the PBD node."
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintValueControl;

                checkBoxGrp -numberOfCheckBoxes 1
                    -label1 "Camera-based painting"
                    -value1 1
                    -annotation "When enabled, brush only paints elements visible to the camera."
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintCameraBasedControl;

                colorSliderGrp -label "Low Color" 
                    -rgb 1.0 0.0 0.0
                    -alphaValue 0.0
                    -annotation "Color representing low paint values"
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintLowColorControl;

                colorSliderGrp -label "High Color" 
                    -rgb 1.0 0.0 0.0
                    -alphaValue 1.0
                    -annotation "Color representing high paint values"
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintHighColorControl;

                rowLayout -numberOfColumns 3 -columnWidth 1 130 -columnWidth 2 65 -columnWidth 3 120;
                    text -label "" -enable false -height 1; // left spacer
                    columnLayout -adjustableColumn true;
                        separator -style "none" -height 2; // top padding
                        button -label "Swap" -width 50 -annotation "Swap low and high colors" -c "VoxelSwapPaintColors()";
                        separator -style "none" -height 2; // bottom padding
                    setParent ..;
                    text -label "" -enable false -height 1; // right spacer
                setParent ..;

                checkBoxGrp -numberOfCheckBoxes 3
                    -label "Component Mask"
                    -columnAlign2 "left" "left"
                    -label1 "+X" -value1 1
                    -label2 "+Y" -value2 1
                    -label3 "+Z" -value3 1
                    -annotation "Limit painting to specific component orientations"
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintDirsRow1;

                checkBoxGrp -numberOfCheckBoxes 3
                    -label ""                      // empty label, but reserve the same label-column width
                    -columnAlign2 "left" "left"
                    -label1 "-X" -value1 1
                    -label2 "-Y" -value2 1
                    -label3 "-Z" -value3 1
                    -annotation "Limit painting to specific component orientations"
                    -cc "VoxelPaintContext_ApplyFromUI()"
                    VoxelPaintDirsRow2;

            setParent ..;
        setParent ..;

    setParent ..;

    setUITemplate -popTemplate;
}

global proc VoxelPaintContext_ApplyFromUI() {
    string $ctx = `currentCtx`;
    if (!`gmatch $ctx "voxelPaintContextCommand*"`)
        return;

    float $radius  = `floatSliderGrp -q -v VoxelPaintRadiusControl`;
    int   $modeIdx = `optionMenuGrp -q -sl VoxelPaintModeControl` - 1;
    float $value  = `floatSliderGrp -q -v VoxelPaintValueControl`;
    int   $cameraBased = `checkBoxGrp -q -v1 VoxelPaintCameraBasedControl`;
    float $lowColor[] = `colorSliderGrp -q -rgb VoxelPaintLowColorControl`;
    float $highColor[] = `colorSliderGrp -q -rgb VoxelPaintHighColorControl`;
    float $lowAlpha[] = `colorSliderGrp -q -alphaValue VoxelPaintLowColorControl`;
    float $highAlpha[] = `colorSliderGrp -q -alphaValue VoxelPaintHighColorControl`;
    int $componentMask = 0;
    if (`checkBoxGrp -q -v1 VoxelPaintDirsRow1`) $componentMask = $componentMask + 1;
    if (`checkBoxGrp -q -v2 VoxelPaintDirsRow1`) $componentMask = $componentMask + 2;
    if (`checkBoxGrp -q -v3 VoxelPaintDirsRow1`) $componentMask = $componentMask + 4;
    if (`checkBoxGrp -q -v1 VoxelPaintDirsRow2`) $componentMask = $componentMask + 8;
    if (`checkBoxGrp -q -v2 VoxelPaintDirsRow2`) $componentMask = $componentMask + 16;
    if (`checkBoxGrp -q -v3 VoxelPaintDirsRow2`) $componentMask = $componentMask + 32;


    catchQuiet(`voxelPaintContextCommand -e -radius $radius $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -mode $modeIdx $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -value $value $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -cameraBased $cameraBased $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -lowColor $lowColor[0] $lowColor[1] $lowColor[2] $lowAlpha[0] $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -highColor $highColor[0] $highColor[1] $highColor[2] $highAlpha[0] $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -componentMask $componentMask $ctx`);
}

global proc VoxelSwapPaintColors() {
    string $ctx = `currentCtx`;
    float $lowColor[]  = `colorSliderGrp -q -rgb VoxelPaintLowColorControl`;
    float $lowAlpha[]    = `colorSliderGrp -q -alphaValue VoxelPaintLowColorControl`;
    float $highColor[] = `colorSliderGrp -q -rgb VoxelPaintHighColorControl`;
    float $highAlpha[]   = `colorSliderGrp -q -alphaValue VoxelPaintHighColorControl`;

    colorSliderGrp -e -rgb $highColor[0] $highColor[1] $highColor[2] -alphaValue $highAlpha[0] VoxelPaintLowColorControl;
    colorSliderGrp -e -rgb $lowColor[0] $lowColor[1] $lowColor[2] -alphaValue $lowAlpha[0] VoxelPaintHighColorControl;

    catchQuiet(`voxelPaintContextCommand -e -lowColor $highColor[0] $highColor[1] $highColor[2] $highAlpha[0] $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -highColor $lowColor[0] $lowColor[1] $lowColor[2] $lowAlpha[0] $ctx`);
}