// This procedure has to have this specific name to be recognized by Maya
// See docs here: https://help.autodesk.com/view/MAYAUL/2022/ENU/?guid=Maya_SDK_Command_plug_ins_Tool_property_sheets_html
global proc VoxelPaintContextProperties() {
    // Required: parent into the Tool Settings property sheet
    string $parent = `toolPropertyWindow -q -location`;

    if (`control -exists $parent`)
        setParent $parent;

    setUITemplate -pushTemplate ToolOptionBoxTemplate;

    columnLayout VoxelPaintContext;

        frameLayout -collapsable false -label "Voxel Paint Settings";
            columnLayout;

                separator -style "none";

                floatSliderGrp -label "Radius"
                    -field true
                    -minValue 0.001
                    -maxValue 200.0
                    -fieldMinValue 0.001
                    -fieldMaxValue 1000.0
                    -value 25.0
                    -adjustableColumn 3
                    -annotation "Brush size in pixels"
                    -cc "VoxelPaintRadiusChanged()"
                    VoxelPaintRadiusControl;

                floatSliderGrp -label "Strength"
                    -field true
                    -minValue 0.000
                    -maxValue 100.0
                    -fieldMinValue 0.000
                    -fieldMaxValue 100.0
                    -value 50.0
                    -adjustableColumn 3
                    -annotation "Brush strength: a percentage of the low-to-high constraint value range set on the PBD node."
                    -cc "VoxelPaintStrengthChanged()"
                    VoxelPaintValueControl;

                checkBoxGrp -numberOfCheckBoxes 1
                    -label1 "Infinite"
                    -value1 0
                    -annotation "When enabled, strength value is infinite"
                    -cc "VoxelPaintInfiniteStrengthChanged()"
                    VoxelPaintInfiniteStrengthControl;

                optionMenuGrp -label "Mode"
                    -adjustableColumn 2
                    -annotation "How a brush stroke affects paint values"
                    -cc "VoxelPaintModeChanged()"
                    VoxelPaintModeControl;

                    menuItem -label "Subtract";
                    menuItem -label "Set";
                    menuItem -label "Add";

                checkBoxGrp -numberOfCheckBoxes 1
                    -label1 "Camera-based painting"
                    -value1 1
                    -annotation "When enabled, brush only paints elements visible to the camera."
                    -cc "VoxelPaintCameraBasedChanged()"
                    VoxelPaintCameraBasedControl;

                colorSliderGrp -label "Low Color" 
                    -rgb 1.0 0.0 0.0
                    -alphaValue 0.0
                    -annotation "Color representing low paint values"
                    -cc "VoxelPaintLowColorChanged()"
                    VoxelPaintLowColorControl;

                colorSliderGrp -label "High Color" 
                    -rgb 1.0 0.0 0.0
                    -alphaValue 1.0
                    -annotation "Color representing high paint values"
                    -cc "VoxelPaintHighColorChanged()"
                    VoxelPaintHighColorControl;

                rowLayout -numberOfColumns 3 -columnWidth 1 130 -columnWidth 2 65 -columnWidth 3 120;
                    text -label "" -enable false -height 1; // left spacer
                    columnLayout -adjustableColumn true;
                        separator -style "none" -height 2; // top padding
                        button -label "Swap" -width 50 -annotation "Swap low and high colors" -c "VoxelSwapPaintColors()";
                        separator -style "none" -height 2; // bottom padding
                    setParent ..;
                    text -label "" -enable false -height 1; // right spacer
                setParent ..;

                frameLayout -collapsable true -collapse true -label "Advanced";
                    checkBoxGrp -numberOfCheckBoxes 3
                        -label "Face Mask"
                        -columnAlign2 "left" "left"
                        -label1 "+X" -value1 1
                        -label2 "+Y" -value2 1
                        -label3 "+Z" -value3 1
                        -annotation "Limit painting to specific faces"
                        -cc "VoxelPaintFaceComponentMaskChanged()"
                        VoxelPaintFaceMask1;

                    checkBoxGrp -numberOfCheckBoxes 3
                        -label ""                      // empty label, but reserve the same label-column width
                        -columnAlign2 "left" "left"
                        -label1 "-X" -value1 1
                        -label2 "-Y" -value2 1
                        -label3 "-Z" -value3 1
                        -annotation "Limit painting to specific faces"
                        -cc "VoxelPaintFaceComponentMaskChanged()"
                        VoxelPaintFaceMask2;

                    separator -style "in" -height 10;

                    checkBoxGrp -numberOfCheckBoxes 4
                        -label "Particle Mask"
                        -columnAlign2 "left" "left"
                        -label1 "0" -value1 1
                        -label2 "1" -value2 1
                        -label3 "2" -value3 1
                        -label4 "3" -value4 1
                        -annotation "Limit painting to specific particles"
                        -cc "VoxelPaintParticleComponentMaskChanged()"
                        VoxelPaintParticleMask1;

                    checkBoxGrp -numberOfCheckBoxes 4
                        -label ""                      // empty label, but reserve the same label-column width
                        -columnAlign2 "left" "left"
                        -label1 "4" -value1 1
                        -label2 "5" -value2 1
                        -label3 "6" -value3 1
                        -label4 "7" -value4 1
                        -annotation "Limit painting to specific particles"
                        -cc "VoxelPaintParticleComponentMaskChanged()"
                        VoxelPaintParticleMask2;
                setParent ..;

            setParent ..;
        setParent ..;

    setParent ..;

    setUITemplate -popTemplate;
}

global proc VoxelPaintRadiusChanged() {
    string $ctx = `currentCtx`;
    float $radius = `floatSliderGrp -q -v VoxelPaintRadiusControl`;
    catchQuiet(`voxelPaintContextCommand -e -radius $radius $ctx`);
}

global proc VoxelPaintStrengthChanged() {
    string $ctx = `currentCtx`;
    float $val = `floatSliderGrp -q -v VoxelPaintValueControl`;
    catchQuiet(`voxelPaintContextCommand -e -value $val $ctx`);
}

global proc VoxelPaintInfiniteStrengthChanged() {
    string $ctx = `currentCtx`;
    int $infiniteStrength = `checkBoxGrp -q -v1 VoxelPaintInfiniteStrengthControl`;
    floatSliderGrp -e -enable (! $infiniteStrength) VoxelPaintValueControl;
    VoxelPaintStrengthChanged();
    if ($infiniteStrength) {
        optionMenuGrp -e -sl 2 VoxelPaintModeControl; // force "Set" mode
        VoxelPaintModeChanged();
    }
    catchQuiet(`voxelPaintContextCommand -e -infStrength $infiniteStrength $ctx`);
}

global proc VoxelPaintModeChanged() {
    string $ctx = `currentCtx`;
    int $modeIdx = `optionMenuGrp -q -sl VoxelPaintModeControl` - 1;
    if ($modeIdx != 1) {
        checkBoxGrp -e -v1 0 VoxelPaintInfiniteStrengthControl; // disable infinite if not in "Set" mode
        VoxelPaintInfiniteStrengthChanged();
        floatSliderGrp -e -enable true VoxelPaintValueControl;  // and re-enable strength control
        VoxelPaintStrengthChanged();
    }
    catchQuiet(`voxelPaintContextCommand -e -mode $modeIdx $ctx`);
}

global proc VoxelPaintCameraBasedChanged() {
    string $ctx = `currentCtx`;
    int $cameraBased = `checkBoxGrp -q -v1 VoxelPaintCameraBasedControl`;
    catchQuiet(`voxelPaintContextCommand -e -cameraBased $cameraBased $ctx`);
}

global proc VoxelPaintLowColorChanged() {
    string $ctx = `currentCtx`;
    float $rgb[] = `colorSliderGrp -q -rgb VoxelPaintLowColorControl`;
    float $alpha[] = `colorSliderGrp -q -alphaValue VoxelPaintLowColorControl`;
    catchQuiet(`voxelPaintContextCommand -e -lowColor $rgb[0] $rgb[1] $rgb[2] $alpha[0] $ctx`);
}

global proc VoxelPaintHighColorChanged() {
    string $ctx = `currentCtx`;
    float $rgb[] = `colorSliderGrp -q -rgb VoxelPaintHighColorControl`;
    float $alpha[] = `colorSliderGrp -q -alphaValue VoxelPaintHighColorControl`;
    catchQuiet(`voxelPaintContextCommand -e -highColor $rgb[0] $rgb[1] $rgb[2] $alpha[0] $ctx`);
}

global proc VoxelPaintFaceComponentMaskChanged() {
    string $ctx = `currentCtx`;
    int $mask = 0;
    if (`checkBoxGrp -q -v1 VoxelPaintFaceMask1`) $mask += 1;
    if (`checkBoxGrp -q -v2 VoxelPaintFaceMask1`) $mask += 2;
    if (`checkBoxGrp -q -v3 VoxelPaintFaceMask1`) $mask += 4;
    if (`checkBoxGrp -q -v1 VoxelPaintFaceMask2`) $mask += 8;
    if (`checkBoxGrp -q -v2 VoxelPaintFaceMask2`) $mask += 16;
    if (`checkBoxGrp -q -v3 VoxelPaintFaceMask2`) $mask += 32;
    catchQuiet(`voxelPaintContextCommand -e -faceComponentMask $mask $ctx`);
}

global proc VoxelPaintParticleComponentMaskChanged() {
    string $ctx = `currentCtx`;
    int $mask = 0;
    if (`checkBoxGrp -q -v1 VoxelPaintParticleMask1`) $mask += 1;
    if (`checkBoxGrp -q -v2 VoxelPaintParticleMask1`) $mask += 2;
    if (`checkBoxGrp -q -v3 VoxelPaintParticleMask1`) $mask += 4;
    if (`checkBoxGrp -q -v4 VoxelPaintParticleMask1`) $mask += 8;
    if (`checkBoxGrp -q -v1 VoxelPaintParticleMask2`) $mask += 16;
    if (`checkBoxGrp -q -v2 VoxelPaintParticleMask2`) $mask += 32;
    if (`checkBoxGrp -q -v3 VoxelPaintParticleMask2`) $mask += 64;
    if (`checkBoxGrp -q -v4 VoxelPaintParticleMask2`) $mask += 128;
    catchQuiet(`voxelPaintContextCommand -e -particleComponentMask $mask $ctx`);
}

global proc VoxelSwapPaintColors() {
    string $ctx = `currentCtx`;
    float $lowColor[]  = `colorSliderGrp -q -rgb VoxelPaintLowColorControl`;
    float $lowAlpha[]    = `colorSliderGrp -q -alphaValue VoxelPaintLowColorControl`;
    float $highColor[] = `colorSliderGrp -q -rgb VoxelPaintHighColorControl`;
    float $highAlpha[]   = `colorSliderGrp -q -alphaValue VoxelPaintHighColorControl`;

    colorSliderGrp -e -rgb $highColor[0] $highColor[1] $highColor[2] -alphaValue $highAlpha[0] VoxelPaintLowColorControl;
    colorSliderGrp -e -rgb $lowColor[0] $lowColor[1] $lowColor[2] -alphaValue $lowAlpha[0] VoxelPaintHighColorControl;

    catchQuiet(`voxelPaintContextCommand -e -lowColor $highColor[0] $highColor[1] $highColor[2] $highAlpha[0] $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -highColor $lowColor[0] $lowColor[1] $lowColor[2] $lowAlpha[0] $ctx`);
}