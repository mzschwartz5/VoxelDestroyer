proc float[] stringToFloatArray(string $s) {
    string $delim = " ";
    string $tokens[];
    int $count = `tokenize $s $delim $tokens`;
    float $out[];
    for ($i = 0; $i < $count; ++$i) {
        $out[$i] = $tokens[$i];
    }
    return $out;
}

// This procedure has to have this specific name to be recognized by Maya
// See docs here: https://help.autodesk.com/view/MAYAUL/2022/ENU/?guid=Maya_SDK_Command_plug_ins_Tool_property_sheets_html
global proc VoxelPaintContextValues(string $toolName) {
    string $parent = (`toolPropertyWindow -q -location`);
    setParent $parent;

    // Override the default title and buttons of the tool property window
    string $field = `toolPropertyWindow -q -field`;
    string $helpBtn  = `toolPropertyWindow -q -helpButton`;
    string $resetBtn = `toolPropertyWindow -q -resetButton`;
    text -e -label "Voxel Paint Tool" $field;
    button -e -c "VoxelPaintContext_Help()" $helpBtn;
    button -e -c "VoxelPaintContext_Reset()" $resetBtn;

    string $ctx = `currentCtx`;
    if (!`gmatch $ctx "voxelPaintContextCommand*"` )
        return;

    float $radius = `voxelPaintContextCommand -q -radius $ctx`;
    int   $mode   = `voxelPaintContextCommand -q -mode $ctx`; // 0=Add,1=Subtract,2=Set
    float $value = `voxelPaintContextCommand -q -value $ctx`;
    int   $cameraBased = `voxelPaintContextCommand -q -cameraBased $ctx`;
    float $lowColor[] = stringToFloatArray(`voxelPaintContextCommand -q -lowColor $ctx`);
    float $highColor[] = stringToFloatArray(`voxelPaintContextCommand -q -highColor $ctx`);
    int $componentMask = `voxelPaintContextCommand -q -componentMask $ctx`;
    // Not sure if MEL supports bitwise ops so... manual extraction
    int $v1 = 0; int $v2 = 0; int $v3 = 0;
    int $v4 = 0; int $v5 = 0; int $v6 = 0;
    if ($componentMask >= 32) { $v6 = 1; $componentMask -= 32; }
    if ($componentMask >= 16) { $v5 = 1; $componentMask -= 16; }
    if ($componentMask >= 8 ) { $v4 = 1; $componentMask -= 8;  }
    if ($componentMask >= 4 ) { $v3 = 1; $componentMask -= 4;  }
    if ($componentMask >= 2 ) { $v2 = 1; $componentMask -= 2;  }
    if ($componentMask >= 1 ) { $v1 = 1; $componentMask -= 1;  }

    if (`control -exists VoxelPaintRadiusControl`)
        floatSliderGrp -e -v $radius VoxelPaintRadiusControl;

    if (`control -exists VoxelPaintModeControl`)
        optionMenuGrp -e -sl ($mode + 1) VoxelPaintModeControl;

    if (`control -exists VoxelPaintValueControl`)
        floatSliderGrp -e -v $value VoxelPaintValueControl;
        
    if (`control -exists VoxelPaintCameraBasedControl`)
        checkBoxGrp -e -v1 $cameraBased VoxelPaintCameraBasedControl;

    if (`control -exists VoxelPaintLowColorControl`)
        colorSliderGrp -e -rgb $lowColor[0] $lowColor[1] $lowColor[2] -alphaValue $lowColor[3] VoxelPaintLowColorControl;

    if (`control -exists VoxelPaintHighColorControl`)
        colorSliderGrp -e -rgb $highColor[0] $highColor[1] $highColor[2] -alphaValue $highColor[3] VoxelPaintHighColorControl;

    if (`control -exists VoxelPaintDirsRow1`)
        checkBoxGrp -e -v1 $v1 -v2 $v2 -v3 $v3 VoxelPaintDirsRow1; 

    if (`control -exists VoxelPaintDirsRow2`)
        checkBoxGrp -e -v1 $v4 -v2 $v5 -v3 $v6 VoxelPaintDirsRow2;

    toolPropertySelect "VoxelPaintContext";
}

global proc VoxelPaintContext_Reset()
{
    // reset UI controls to defaults
    floatSliderGrp -e -v 50.0 VoxelPaintRadiusControl;
    optionMenuGrp -e -sl 1 VoxelPaintModeControl;
    floatSliderGrp -e -v 0.5 VoxelPaintValueControl;
    checkBoxGrp -e -v1 1 VoxelPaintCameraBasedControl;
    colorSliderGrp -e -rgb 1.0 0.0 0.0 -alphaValue 0.0 VoxelPaintLowColorControl;
    colorSliderGrp -e -rgb 1.0 0.0 0.0 -alphaValue 1.0 VoxelPaintHighColorControl;
    checkBoxGrp -e -v1 1 -v2 1 -v3 1 VoxelPaintDirsRow1;
    checkBoxGrp -e -v1 1 -v2 1 -v3 1 VoxelPaintDirsRow2;

    // push defaults into the active context (if active)
    string $ctx = `currentCtx`;
    catchQuiet(`voxelPaintContextCommand -e -radius 50 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -mode 0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -value 0.5 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -cameraBased 1 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -lowColor 1.0 0.0 0.0 0.0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -highColor 1.0 0.0 0.0 1.0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -componentMask 63 $ctx`); // all directions enabled
}

global proc VoxelPaintContext_Help()
{
    // TODO: better URL?
    showHelp -a "http://github.com/mzschwartz5/VoxelDestroyer";
}