proc float[] stringToFloatArray(string $s) {
    string $delim = " ";
    string $tokens[];
    int $count = `tokenize $s $delim $tokens`;
    float $out[];
    for ($i = 0; $i < $count; ++$i) {
        $out[$i] = $tokens[$i];
    }
    return $out;
}

global proc VoxelPaintApplyFaceMaskToUI() {
    string $ctx = `currentCtx`;
    int $componentMask = `voxelPaintContextCommand -q -faceComponentMask $ctx`;
    int $v1 = 0; int $v2 = 0; int $v3 = 0; int $v4 = 0; int $v5 = 0; int $v6 = 0;
    if ($componentMask >= 32) { $v6 = 1; $componentMask -= 32; }
    if ($componentMask >= 16) { $v5 = 1; $componentMask -= 16; }
    if ($componentMask >= 8 ) { $v4 = 1; $componentMask -= 8;  }
    if ($componentMask >= 4 ) { $v3 = 1; $componentMask -= 4;  }
    if ($componentMask >= 2 ) { $v2 = 1; $componentMask -= 2;  }
    if ($componentMask >= 1 ) { $v1 = 1; $componentMask -= 1;  }
    checkBoxGrp -e -v1 $v2 -v2 $v4 -v3 $v6 VoxelPaintFaceMask1;
    checkBoxGrp -e -v1 $v1 -v2 $v3 -v3 $v5 VoxelPaintFaceMask2;
}

global proc VoxelPaintApplyParticleMaskToUI() {
    string $ctx = `currentCtx`;
    int $componentMask = `voxelPaintContextCommand -q -particleComponentMask $ctx`;
    int $v1 = 0; int $v2 = 0; int $v3 = 0; int $v4 = 0; int $v5 = 0; int $v6 = 0; int $v7 = 0; int $v8 = 0;
    if ($componentMask >= 128) { $v8 = 1; $componentMask -= 128; }
    if ($componentMask >= 64 ) { $v7 = 1; $componentMask -= 64;  }
    if ($componentMask >= 32) { $v6 = 1; $componentMask -= 32; }
    if ($componentMask >= 16) { $v5 = 1; $componentMask -= 16; }
    if ($componentMask >= 8 ) { $v4 = 1; $componentMask -= 8;  }
    if ($componentMask >= 4 ) { $v3 = 1; $componentMask -= 4;  }
    if ($componentMask >= 2 ) { $v2 = 1; $componentMask -= 2;  }
    if ($componentMask >= 1 ) { $v1 = 1; $componentMask -= 1;  }
    checkBoxGrp -e -v1 $v1 -v2 $v2 -v3 $v3 -v4 $v4 VoxelPaintParticleMask1;
    checkBoxGrp -e -v1 $v5 -v2 $v6 -v3 $v7 -v4 $v8 VoxelPaintParticleMask2;
}
// This procedure has to have this specific name to be recognized by Maya
// See docs here: https://help.autodesk.com/view/MAYAUL/2022/ENU/?guid=Maya_SDK_Command_plug_ins_Tool_property_sheets_html
global proc VoxelPaintContextValues(string $toolName) {
    string $parent = (`toolPropertyWindow -q -location`);
    setParent $parent;

    // Override the default title and buttons of the tool property window
    string $field = `toolPropertyWindow -q -field`;
    string $helpBtn  = `toolPropertyWindow -q -helpButton`;
    string $resetBtn = `toolPropertyWindow -q -resetButton`;
    text -e -label "Voxel Paint Tool" $field;
    button -e -c "VoxelPaintContext_Help()" $helpBtn;
    button -e -c "VoxelPaintContext_Reset()" $resetBtn;

    string $ctx              = `currentCtx`;
    float  $radius           = `voxelPaintContextCommand -q -radius $ctx`;
    float  $value            = `voxelPaintContextCommand -q -value $ctx`;
    int    $infiniteStrength = `voxelPaintContextCommand -q -infStrength $ctx`;
    int    $mode             = `voxelPaintContextCommand -q -mode $ctx`; // 0=Subtract, 1=Set, 2=Add
    int    $cameraBased      = `voxelPaintContextCommand -q -cameraBased $ctx`;
    float  $lowColor[]       = stringToFloatArray(`voxelPaintContextCommand -q -lowColor $ctx`);
    float  $highColor[]      = stringToFloatArray(`voxelPaintContextCommand -q -highColor $ctx`);

    floatSliderGrp -e -v $radius VoxelPaintRadiusControl;
    floatSliderGrp -e -v $value VoxelPaintValueControl;
    checkBoxGrp -e -v1 $infiniteStrength VoxelPaintInfiniteStrengthControl;
    optionMenuGrp -e -sl ($mode + 1) VoxelPaintModeControl; // UI indices are 1-based
    checkBoxGrp -e -v1 $cameraBased VoxelPaintCameraBasedControl;
    colorSliderGrp -e -rgb $lowColor[0] $lowColor[1] $lowColor[2] -alphaValue $lowColor[3] VoxelPaintLowColorControl;
    colorSliderGrp -e -rgb $highColor[0] $highColor[1] $highColor[2] -alphaValue $highColor[3] VoxelPaintHighColorControl;

    VoxelPaintApplyFaceMaskToUI();
    VoxelPaintApplyParticleMaskToUI();

    toolPropertySelect "VoxelPaintContext";
}

global proc VoxelPaintContext_Reset()
{
    // reset UI controls to defaults
    floatSliderGrp -e -v 25.0 VoxelPaintRadiusControl;
    floatSliderGrp -e -v 50.0 VoxelPaintValueControl;
    checkBoxGrp -e -v1 0 VoxelPaintInfiniteStrengthControl;
    optionMenuGrp -e -sl 2 VoxelPaintModeControl; // UI indices are 1-based, so this is "Set" mode
    checkBoxGrp -e -v1 1 VoxelPaintCameraBasedControl;
    colorSliderGrp -e -rgb 1.0 0.0 0.0 -alphaValue 0.0 VoxelPaintLowColorControl;
    colorSliderGrp -e -rgb 1.0 0.0 0.0 -alphaValue 1.0 VoxelPaintHighColorControl;
    checkBoxGrp -e -v1 1 -v2 1 -v3 1 VoxelPaintFaceMask1;
    checkBoxGrp -e -v1 1 -v2 1 -v3 1 VoxelPaintFaceMask2;
    checkBoxGrp -e -v1 1 -v2 1 -v3 1 -v4 1 VoxelPaintParticleMask1;
    checkBoxGrp -e -v1 1 -v2 1 -v3 1 -v4 1 VoxelPaintParticleMask2;

    // push defaults into the active context (if active)
    string $ctx = `currentCtx`;
    catchQuiet(`voxelPaintContextCommand -e -radius 25.0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -value 50.0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -infStrength 0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -mode 1 $ctx`); // "Set" mode
    catchQuiet(`voxelPaintContextCommand -e -cameraBased 1 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -lowColor 1.0 0.0 0.0 0.0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -highColor 1.0 0.0 0.0 1.0 $ctx`);
    catchQuiet(`voxelPaintContextCommand -e -faceComponentMask 63 $ctx`); // all directions enabled
    catchQuiet(`voxelPaintContextCommand -e -particleComponentMask 255 $ctx`); // all directions enabled
}

global proc VoxelPaintContext_Help()
{
    showHelp -a "https://github.com/mzschwartz5/cubit?tab=readme-ov-file#paint-tool-settings";
}